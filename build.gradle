// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.5.30'
    ext.dokka_version = '0.9.16'
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.18.0'
        classpath 'com.android.tools.build:gradle:7.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jlleitschuh.gradle.ktlint' version '10.0.0'
}

ktlint {
    version = '0.40.0'
}

allprojects {
    group = GROUP
    version = VERSION_NAME

    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

subprojects { project ->
    apply plugin: 'maven-publish'

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
        plugin.extension.compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    publishing {
        publications {
            release(MavenPublication) {
            }
        }
    }
}

apply from: rootProject.file('dependencies.gradle')
