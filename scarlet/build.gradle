apply plugin: 'kotlin'
apply plugin: 'java-library'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-kapt'

sourceSets {
    main {
        java.srcDir "${buildDir}/generated/source/kapt/"
    }
}

dependencies {
    api project(':scarlet-core')

    implementation project(':scarlet-core-internal')
    implementation project(':state-machine')
    implementation project(':scarlet-message-adapter-builtin')
    implementation project(':scarlet-stream-adapter-builtin')
    implementation rootProject.ext.rxJava
    implementation rootProject.ext.rxKotlin
    implementation rootProject.ext.kotlinStdlib
    implementation rootProject.ext.dagger
    kapt rootProject.ext.daggerCompiler
    compileOnly rootProject.ext.jsr250Annotations

    testImplementation project(':scarlet-test-utils')
    testImplementation project(':scarlet-stream-adapter-rxjava2')
    testImplementation rootProject.ext.junit
    testImplementation rootProject.ext.mockito
    testImplementation rootProject.ext.kotlinReflect
    testImplementation rootProject.ext.assertJ
}
repositories {
    mavenCentral()
    google()
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.tinder'
            version version
            artifactId project.getName()
            artifact sourcesJar
            artifact javadocJar
            from components.java
        }
    }
}

artifactory {
    contextUrl = 'https://tinder.jfrog.io/tinder'
    publish {
        repository {
            repoKey = 'libs-release-local'
            username = System.getenv("ARTIFACTORY_USER")
            password = System.getenv("ARTIFACTORY_PASSWORD")
            maven = true
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishPom = true
        }
    }
    clientConfig.info.setBuildName('com.tinder.scarlet')
}
